plugins {
    id 'java-gradle-plugin'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly("org.projectlombok:lombok:1.18.42")
	annotationProcessor("org.projectlombok:lombok:1.18.42")

    testImplementation 'junit:junit:4.13'
}

gradlePlugin {
    plugins {
        greeting {
            id = 'shell-package'
            implementationClass = 'net.tetrakoopa.gradle.plugin.shell.ShellPackagePlugin'
        }
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.FAIL
    archiveBaseName = "shell-package-plugin"
}

sourceSets {
    main {
        resources {
            srcDirs "tool"
        }
    }
}


def functionalTest = sourceSets.create('functionalTest')
gradlePlugin.testSourceSets(functionalTest)

configurations[functionalTest.implementationConfigurationName].extendsFrom(configurations.testImplementation)

def functionalTestTask = tasks.register('functionalTest', Test) {
    testClassesDirs = functionalTest.output.classesDirs
    classpath = configurations[functionalTest.runtimeClasspathConfigurationName] + functionalTest.output
}

tasks.check.configure {
    dependsOn(functionalTestTask)
}
